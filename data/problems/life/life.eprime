language ESSENCE' 1.0

given nbDim, nbTime : int

letting PDIM be int(-1..nbDim+2)                $ padded by 2
letting DIM  be int(1..nbDim)
letting TIME be int(1..nbTime)

find l : matrix indexed by [TIME, PDIM, PDIM] of int(0,1)

maximising sum([ l[1,i,j] | i,j : DIM ])

such that

    $ padded parts are always dead
    forall t : TIME . forall i, j : PDIM .
        (!(i in DIM /\ j in DIM)) -> l[t,i,j] = false,

    $ any two layers not equal
    forall t : TIME . forall t2 : int(t+1..nbTime) .
        exists i,j : DIM . l[t,i,j] != l[t2,i,j],
    
    $ first layer lex less than all other layers
    forall t : int(2..nbTime) .
        flatten(l[1,..,..]) <lex flatten(l[t,..,..]),

    $ symmetry breaking on first layer
    
    [ l[1,i,j] | i, j : DIM ] <=lex [ l[1,i2,j2]
                                    | i, j : DIM
                                    , i2 : int(nbDim+1- i)
                                    , j2 : int(j)
                                    ],

    [ l[1,i,j] | i, j : DIM ] <=lex [ l[1,i2,j2]
                                    | i, j : DIM
                                    , i2 : int(i)
                                    , j2 : int(nbDim+1- j)
                                    ],

    [ l[1,i,j] | i, j : DIM ] <=lex [ l[1,i2,j2]
                                    | i, j : DIM
                                    , i2 : int(nbDim+1- i)
                                    , j2 : int(nbDim+1- j)
                                    ],

    [ l[1,i,j] | i, j : DIM ] <=lex [ l[1,i2,j2]
                                    | i, j : DIM
                                    , i2 : int(j)
                                    , j2 : int(i)
                                    ],

    [ l[1,i,j] | i, j : DIM ] <=lex [ l[1,i2,j2]
                                    | i, j : DIM
                                    , i2 : int(nbDim+1- j)
                                    , j2 : int(i)
                                    ],

    [ l[1,i,j] | i, j : DIM ] <=lex [ l[1,i2,j2]
                                    | i, j : DIM
                                    , i2 : int(j)
                                    , j2 : int(nbDim+1- i)
                                    ],

    [ l[1,i,j] | i, j : DIM ] <=lex [ l[1,i2,j2]
                                    | i, j : DIM
                                    , i2 : int(nbDim+1- j)
                                    , j2 : int(nbDim+1- i)
                                    ],

    $ rules from https://en.wikipedia.org/wiki/Conway%27s_Game_of_Life#Rules
    forall t : int(1..nbTime) . forall i, j : int(0..nbDim+1) .
        exists tplus1 : int( ((t%nbTime)+1) ) .
        makeTable(and(
            [
                $ Rule 1: Any live cell with fewer than two live neighbours dies, as if caused by under-population.
                (l[t,i,j] = true) /\
                (sum([ l[t,i-1,j-1], l[t,i-1,j], l[t,i-1,j+1]
                     , l[t,i  ,j-1],             l[t,i  ,j+1]
                     , l[t,i+1,j-1], l[t,i+1,j], l[t,i+1,j+1]
                     ]) < 2)
                -> l[tplus1,i,j] = false
            ,
                $ Rule 2: Any live cell with two or three live neighbours lives on to the next generation.
                (l[t,i,j] = true) /\
                (sum([ l[t,i-1,j-1], l[t,i-1,j], l[t,i-1,j+1]
                     , l[t,i  ,j-1],             l[t,i  ,j+1]
                     , l[t,i+1,j-1], l[t,i+1,j], l[t,i+1,j+1]
                     ]) in int(2,3))
                -> l[tplus1,i,j] = true
            ,
                $ Rule 3: Any live cell with more than three live neighbours dies, as if by over-population.
                (l[t,i,j] = true) /\
                (sum([ l[t,i-1,j-1], l[t,i-1,j], l[t,i-1,j+1]
                     , l[t,i  ,j-1],             l[t,i  ,j+1]
                     , l[t,i+1,j-1], l[t,i+1,j], l[t,i+1,j+1]
                     ]) > 3)
                -> l[tplus1,i,j] = false
            ,
                $ Rule 4: Any dead cell with exactly three live neighbours becomes a live cell, as if by reproduction.
                (l[t,i,j] = false) /\
                (sum([ l[t,i-1,j-1], l[t,i-1,j], l[t,i-1,j+1]
                     , l[t,i  ,j-1],             l[t,i  ,j+1]
                     , l[t,i+1,j-1], l[t,i+1,j], l[t,i+1,j+1]
                     ]) = 3)
                -> l[tplus1,i,j] = true
            ,
                $ Frame: Any dead cell with not three live neighbours stays dead.
                (l[t,i,j] = false) /\
                (sum([ l[t,i-1,j-1], l[t,i-1,j], l[t,i-1,j+1]
                     , l[t,i  ,j-1],             l[t,i  ,j+1]
                     , l[t,i+1,j-1], l[t,i+1,j], l[t,i+1,j+1]
                     ]) != 3)
                -> l[tplus1,i,j] = false
            ])),

    true
